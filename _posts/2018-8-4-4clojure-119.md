---
layout: post
title: 4clojure 119 - Win at Tic-Tac-Toe
---

As in Problem 73, a tic-tac-toe board is represented by a two dimensional vector. X is represented by :x, O is represented by :o, and empty is represented by :e. Create a function that accepts a game piece and board as arguments, and returns a set (possibly empty) of all valid board placements of the game piece which would result in an immediate win.

Board coordinates should be as in calls to get-in. For example, `[0 1]` is the topmost row, center position.

<pre><code class="language-klipse">(ns live.test
  (:require [cljs.test :refer-macros [deftest is run-tests]]))

(defn ttt [p board]
  (let [win? #(let [b (concat % (apply map list %) 
                    [(map nth % (range)) (map nth (map reverse %) (range))])]
                (some #{[p p p]} b))]
    (set
      (for [y (range 3), x (range 3),
            :when (and (= :e (get-in board [y x]))
                       (win? (assoc-in board [y x] p)))]
        [y x]))))

(deftest test-119
  (is (= (ttt :x [[:o :e :e] 
           [:o :x :o] 
           [:x :x :e]])
   #{[2 2] [0 1] [0 2]}))
  (is (= (ttt :x [[:x :o :o] 
           [:x :x :e] 
           [:e :o :e]])
   #{[2 2] [1 2] [2 0]}))
  (is (= (ttt :x [[:x :e :x] 
           [:o :x :o] 
           [:e :o :e]])
   #{[2 2] [0 1] [2 0]}))
  (is (= (ttt :x [[:x :x :o] 
           [:e :e :e] 
           [:e :e :e]])
   #{}))
  (is (= (ttt :o [[:x :x :o] 
           [:o :e :o] 
           [:x :e :e]])
   #{[2 2] [1 1]})))

(run-tests)
</code></pre>
