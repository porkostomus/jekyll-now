---
layout: post
title: 4clojure 114 - Global take-while
---

`take-while` is great for filtering sequences, but it limited: you can only examine a single item of the sequence at a time. What if you need to keep track of some state as you go over the sequence?

Write a function which accepts an integer n, a predicate p, and a sequence. It should return a lazy sequence of items in the list up to, but not including, the nth item that satisfies the predicate.

<pre><code class="language-klipse">(ns live.test
  (:require [cljs.test :refer-macros [deftest is run-tests]]))
  
(defn gtw [n p [x & r]]
  (lazy-seq
    (when x
      (let [remaining (if (p x) (dec n) n)]
        (when (pos? remaining)
          (cons x (gtw remaining p r)))))))

(deftest test-114
  (is (= [2 3 5 7 11 13]
         (gtw 4 #(= 2 (mod % 3))
              [2 3 5 7 11 13 17 19 23])))
  (is (= ["this" "is" "a" "sentence"]
         (gtw 3 #(some #{\i} %)
              ["this" "is" "a" "sentence" "i" "wrote"])))
  (is (= ["this" "is"]
         (gtw 1 #{"a"}
              ["this" "is" "a" "sentence" "i" "wrote"]))))
  
(run-tests)
</code></pre>
