---
layout: post
title: 4clojure 84-86
---

## 84 - Transitive Closure

Write a function which generates the [transitive closure](http://en.wikipedia.org/wiki/Transitive_closure) of a [binary relation](http://en.wikipedia.org/wiki/Binary_relation). The relation will be represented as a set of 2 item vectors.

<pre><code class="language-klipse">(ns live.test
  (:require [cljs.test :refer-macros [deftest is testing run-tests]]))
  
(defn trans-closure [r]
  (loop [s r]
    (let [n (into s
                  (for [[a b] s [c d] s 
                        :when (= b c)] 
                    [a d]))]
      (if (= n s) n (recur n)))))

(deftest test-84
  (is (let [divides #{[8 4] [9 3] [4 2] [27 9]}]
    (= (trans-closure divides) #{[4 2] [8 4] [8 2] [9 3] [27 9] [27 3]})))
  (is (let [more-legs
      #{["cat" "man"] ["man" "snake"] ["spider" "cat"]}]
  (= (trans-closure more-legs)
     #{["cat" "man"] ["cat" "snake"] ["man" "snake"]
       ["spider" "cat"] ["spider" "man"] ["spider" "snake"]})))
  (is (let [progeny
      #{["father" "son"] ["uncle" "cousin"] ["son" "grandson"]}]
  (= (trans-closure progeny)
     #{["father" "son"] ["father" "grandson"]
       ["uncle" "cousin"] ["son" "grandson"]}))))

(defn powerset [s]
  (reduce #(into % (for [subset %] (conj subset %2))) #{#{}} s))

(deftest test-85
  (is (= (powerset #{1 :a}) #{#{1 :a} #{:a} #{} #{1}}))
  (is (= (powerset #{}) #{#{}}))
  (is (= (powerset #{1 2 3}) #{#{} #{1} #{2} #{3} #{1 2} #{1 3} #{2 3} #{1 2 3}}))
  (is (= (count (powerset (into #{} (range 10)))) 1024)))

(defn happy [n]
  (loop [n n
         s #{}]
    (let [x (apply + (map #(let [i (- (int %) (int \0))] (* i i)) (str n)))]
      (cond (= x 1) true
            (s x) false
            :else (recur x (conj s x))))))

(deftest test-86
  (is (= (happy 7) true))
  (is (= (happy 986543210) true))
  (is (= (happy 2) false))
  (is (= (happy 3) false)))

(run-tests)
</code></pre>
