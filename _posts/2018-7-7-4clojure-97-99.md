---
layout: post
title: 4clojure 97-99
---

## 97 - Pascal's Triangle

[Pascal's triangle](http://en.wikipedia.org/wiki/Pascal%27s_triangle) is a triangle of numbers computed using the following rules:

- The first row is 1.
- Each successive row is computed by adding together adjacent numbers in the row above, and adding a 1 to the beginning and end of the row.

Write a function which returns the nth row of Pascal's Triangle. 

<pre><code class="language-klipse">(ns live.test
  (:require [cljs.test :refer-macros [deftest is testing run-tests]]))

(defn pascal [n]
  (loop [x 1 l [1]]
    (if (= x n) l
      (recur (inc x)(vec (map + (cons 0 l)(conj l 0)))))))

(deftest test-97
  (is (= (pascal 1) [1]))
  (is (= (map pascal (range 1 6))
   [     [1]
        [1 1]
       [1 2 1]
      [1 3 3 1]
     [1 4 6 4 1]]))
  (is (= (pascal 11)
   [1 10 45 120 210 252 210 120 45 10 1])))

; 98 - Equivalence Classes

; A function f defined on a domain D induces an equivalence relation on D, as follows: a is equivalent to b with respect to f if and only if (f a) is equal to (f b). Write a function with arguments f and D that computes the equivalence classes of D with respect to f.

(defn classes [f c]
  (set (map set (vals (group-by f c)))))

(deftest test-98
  (is (= (classes #(* % %) #{-2 -1 0 1 2})
   #{#{0} #{1 -1} #{2 -2}}))
  (is (= (classes #(rem % 3) #{0 1 2 3 4 5 })
   #{#{0 3} #{1 4} #{2 5}}))
  (is (= (classes identity #{0 1 2 3 4})
   #{#{0} #{1} #{2} #{3} #{4}}))
  (is (= (classes (constantly true) #{0 1 2 3 4})
   #{#{0 1 2 3 4}})))

; 99 - Product Digits

; Write a function which multiplies two numbers and returns the result as a sequence of its digits.

(defn digits [x y]
  (map #(- (int %) (int \0)) (str (* x y))))

(deftest test-99
  (is (= (digits 1 1) [1]))
  (is (= (digits 99 9) [8 9 1]))
  (is (= (digits 999 99) [9 8 9 0 1])))
       
(run-tests)
</code></pre>
