---
layout: post
title: 4clojure 108-110
---

## 108 Lazy Searching

Given any number of sequences, each sorted from smallest to largest, find the smallest single number which appears in all of the sequences. The sequences may be infinite, so be careful to search lazily.

<pre><code class="language-klipse">(ns live.test
  (:require [cljs.test :refer-macros [deftest is run-tests]]))
  
(defn lazy [& s]
  (loop [v (vec s)]
    (let [first-vals (map first v)
          smallest (apply min first-vals)]
      (if (= smallest (apply max first-vals))
        smallest
        (let [i (apply min-key #(nth first-vals %) (range (count v)))]
          (recur (update-in v [i] next)))))))

(deftest test-108
  (is (= 3 (lazy [3 4 5])))
  (is (= 4 (lazy [1 2 3 4 5 6 7] [0.5 3/2 4 19])))
  (is (= 64 (lazy (map #(* % % %) (range))
          (filter #(zero? (bit-and % (dec %))) (range))
          (iterate inc 20)))))

;;TODO: (is (= 7 (lazy (range) (range 0 100 7/6) [2 3 5 7 11 13])))

; 110 Sequence of pronunciations
; Write a function that returns a lazy sequence of "pronunciations" of a sequence of numbers. A pronunciation of each element in the sequence consists of the number of repeating identical numbers and the number itself. For example, [1 1] is pronounced as [2 1] ("two ones"), which in turn is pronounced as [1 2 1 1] ("one two, one one").

; Your function should accept an initial sequence of numbers, and return an infinite lazy sequence of pronunciations, each element being a pronunciation of the previous element.

(defn seq-prons [s]
  (next (iterate #(mapcat (juxt count first)
                    (partition-by identity %)) s)))

(deftest test-110
  (is (= [[1 1] [2 1] [1 2 1 1]] (take 3 (seq-prons [1]))))
  (is (= [3 1 2 4] (first (seq-prons [1 1 1 4 4]))))
  (is (= [1 1 1 3 2 1 3 2 1 1] (nth (seq-prons [1]) 6)))
  (is (= 338 (count (nth (seq-prons [3 2]) 15)))))

(run-tests)
</code></pre>
