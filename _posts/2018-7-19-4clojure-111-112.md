---
layout: post
title: 4clojure 111-112
---

## 111 Crossword puzzle

Write a function that takes a string and a partially-filled crossword puzzle board, and determines if the input string can be legally placed onto the board. 

The crossword puzzle board consists of a collection of partially-filled rows. Empty spaces are denoted with an underscore (_), unusable spaces are denoted with a hash symbol (#), and pre-filled spaces have a character in place; the whitespace characters are for legibility and should be ignored. 

For a word to be legally placed on the board: 
- It may use empty spaces (underscores) 
- It may use but must not conflict with any pre-filled characters. 
- It must not use any unusable spaces (hashes). 
- There must be no empty spaces (underscores) or extra characters before or after the word (the word may be bound by unusable spaces though). 
- Characters are not case-sensitive. 
- Words may be placed vertically (proceeding top-down only), or horizontally (proceeding left-right only).

<pre><code class="language-klipse">(ns live.test
  (:require [cljs.test :refer-macros [deftest is run-tests]]))
  
(defn cw [word board]
  (let [across (map #(clojure.string/escape % {\space "", \_ \.}) board)
        down (apply map str across)]   ;; transpose the board so down becomes across
    (string? (->> (concat across down)
                  (mapcat #(clojure.string/split % #"#"))
                  (some #(re-matches (re-pattern %) word))))))

(deftest test-111
  (is (= true  (cw "the" ["_ # _ _ e"])))
  (is (= false (cw "the" ["c _ _ _"
                    "d _ # e"
                    "r y _ _"])))
  (is (= true  (cw "joy" ["c _ _ _"
                    "d _ # e"
                    "r y _ _"])))
  (is (= false (cw "joy" ["c o n j"
                    "_ _ y _"
                    "r _ _ #"])))
  (is (= true  (cw "clojure" ["_ _ _ # j o y"
                        "_ _ o _ _ _ _"
                        "_ _ f _ # _ _"]))))

; 112 Sequs Horribilis
; Create a function which takes an integer and a nested collection of integers as arguments. Analyze the elements of the input collection and return a sequence which maintains the nested structure, and which includes all elements starting from the head whose sum is less than or equal to the input integer.

(defn sh [n [x & r]]
  (if x
    (if (sequential? x)
      (let [sub (sh n x)]
        (cons sub (sh (- n (reduce + (flatten sub))) r)))
      (if (<= x n)
        (cons x (sh (- n x) r))
        ()))))

(deftest test-112
  (is (=  (sh 10 [1 2 [3 [4 5] 6] 7])
   '(1 2 (3 (4)))))
  (is (=  (sh 30 [1 2 [3 [4 [5 [6 [7 8]] 9]] 10] 11])
   '(1 2 (3 (4 (5 (6 (7))))))))
  (is (=  (sh 9 (range))
   '(0 1 2 3)))
  (is (=  (sh 1 [[[[[1]]]]])
   '(((((1)))))))
  (is (=  (sh 0 [1 2 [3 [4 5] 6] 7])
   '()))
  (is (=  (sh 0 [0 0 [0 [0]]])
   '(0 0 (0 (0)))))
  (is (=  (sh 1 [-10 [1 [2 3 [4 5 [6 7 [8]]]]]])
   '(-10 (1 (2 3 (4)))))))

(run-tests)
</code></pre>
