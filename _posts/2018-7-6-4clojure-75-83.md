---
layout: post
title: 4clojure 75-83
---

## 75 - Euler's Totient Function

Two numbers are coprime if their greatest common divisor equals 1. Euler's totient function f(x) is defined as the number of positive integers less than x which are coprime to x. The special case f(1) equals 1. Write a function which calculates Euler's totient function.

<pre><code class="language-klipse">(ns live.test
  (:require [cljs.test :refer-macros [deftest is testing run-tests]]))
  
(defn totient [a]
  (count
    (for [b (range a)
          :when (not-any? #(= 0 (rem a %) (rem b %)) (range 2 a))]
      b)))

(deftest test-75
  (is (= (totient 1) 1))
  (is (= (totient 10) (count '(1 3 7 9)) 4))
  (is (= (totient 40) 16))
  (is (= (totient 99) 60)))
  
(defn anagram [c]
  (set (for [[_ g] (group-by frequencies c)
            :when (next g)]               
        (set g))))

(deftest test-77
  (is (= (anagram ["meat" "mat" "team" "mate" "eat"]) #{#{"meat" "team" "mate"}}))
  (is (= (anagram ["veer" "lake" "item" "kale" "mite" "ever"]) #{#{"veer" "ever"} #{"lake" "kale"} #{"mite" "item"}})))
  
(defn my-trampoline [f & x]
  (if (fn? f)
    (my-trampoline (apply f x))
    f))

(deftest test-78
  (is (= (letfn [(triple [x] #(sub-two (* 3 x)))
     (sub-two [x] #(stop?(- x 2)))
       (stop? [x] (if (> x 50) x #(triple x)))]
         (my-trampoline triple 2)) 82))
  (is (= (letfn [(my-even? [x] (if (zero? x) true #(my-odd? (dec x))))
  (my-odd? [x] (if (zero? x) false #(my-even? (dec x))))]
    (map (partial my-trampoline my-even?) (range 6)))
    [true false true false true false])))
  
(defn tri-path [s]
    (first
     (reduce
      #(map + (map min (butlast %1) (rest %1)) %2)
      (reverse s))))

(deftest test-79
  (is (= (tri-path [[1] [2 4] [5 1 4] [2 3 4 5]]) (+ 1 2 1 3) 7))
  (is (= (tri-path [[3] [2 4] [1 9 3] [9 9 2 4] [4 6 6 7 8] [5 7 3 5 1 4]]) (+ 3 4 3 2 7 1) 20)))
  
(defn perfect-nums [x]
  (= x (apply + (filter #(= 0 (mod x %)) (range 1 x)))))
  
(defn perfect-nums [x]
  (= x (apply + (filter #(= 0 (mod x %)) (range 1 x)))))

(deftest test-80
  (is (= (perfect-nums 6) true))
  (is (= (perfect-nums 7) false))
  (is (= (perfect-nums 496) true))
  (is (= (perfect-nums 500) false))
  (is (= (perfect-nums 8128) true)))
  
(defn set-sect [a b]
  (set (filter a b)))

(deftest test-81
  (is (= (set-sect #{0 1 2 3} #{2 3 4 5}) #{2 3}))
  (is (= (set-sect #{0 1 2} #{3 4 5}) #{}))
  (is (= (set-sect #{:a :b :c :d} #{:c :e :a :f :d}) #{:a :c :d})))
  
(defn word-chain [s]
  (or (some (fn [w]
              ((fn f [a s]
                 (or (empty? s)
                     (some #(if (loop [[a & b :as c] (seq a) [d & e :as g] (seq %)]
                                  (if (= a d)
                                    (recur b e)
                                    (or (= b e) (= b g) (= c e))))
                              (f % (disj s %)))
                           s)))
               w (disj s w)))
            s)
      false))

(deftest test-82
  (is (= true (word-chain #{"hat" "coat" "dog" "cat" "oat" "cot" "hot" "hog"})))
  (is (= false (word-chain #{"cot" "hot" "bat" "fat"})))
  (is (= false (word-chain #{"to" "top" "stop" "tops" "toss"})))
  (is (= true (word-chain #{"spout" "do" "pot" "pout" "spot" "dot"})))
  (is (= true (word-chain #{"share" "hares" "shares" "hare" "are"})))
  (is (= false (word-chain #{"share" "hares" "hare" "are"}))))
  
(defn half-truth [& vs]
  (true? (and (some not vs)
         (some identity vs))))

(deftest test-83
  (is (= false (half-truth false false)))
  (is (= true (half-truth true false)))
  (is (= false (half-truth true)))
  (is (= true (half-truth false true false)))
  (is (= false (half-truth true true true)))
  (is (= true (half-truth true true true false))))
  
(run-tests)
</code></pre>
