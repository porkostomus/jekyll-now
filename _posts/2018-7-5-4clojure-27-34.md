---
layout: post
title: 4clojure-27-34
---

<pre><code class="language-klipse">(ns live.test
  (:require [cljs.test :refer-macros [deftest is testing run-tests]]))
  
(defn mypal [s]
  (= (seq s) (reverse (seq s))))

(deftest test-27
  (is (false? (mypal '(1 2 3 4 5))))
  (is (true? (mypal "racecar")))
  (is (true? (mypal [:foo :bar :foo])))
  (is (true? (mypal '(1 1 3 3 1 1))))
  (is (false? (mypal '(:a :b :c)))))

(defn makeflat [s]
  (filter (complement sequential?)
    (rest (tree-seq sequential? seq s))))

(deftest test-28
  (is (= (makeflat '((1 2) 3 [4 [5 6]])) '(1 2 3 4 5 6)))
  (is (= (makeflat ["a" ["b"] "c"]) '("a" "b" "c")))
  (is (= (makeflat '((((:a))))) '(:a))))

(defn getcaps [s]
  (apply str (re-seq #"[A-Z]" s)))

(deftest test-29
  (is (= (getcaps "HeLlO, WoRlD!") "HLOWRD"))
  (is (empty? (getcaps "nothing")))
  (is (= (getcaps "$#A(*&987Zf") "AZ")))

(defn compress [s]
  (map first
    (partition-by identity s)))

(deftest test-30
  (is (= (apply str (compress "Leeeeeerrroyyy")) "Leroy"))
  (is (= (compress [1 1 2 3 3 2 2 3]) '(1 2 3 2 3)))
  (is (= (compress [[1 2] [1 2] [3 4] [1 2]]) '([1 2] [3 4] [1 2]))))
  
(defn pack [s]
  (partition-by identity s))

(deftest test-31
  (is (= (pack [1 1 2 1 1 1 3 3]) '((1 1) (2) (1 1 1) (3 3))))
  (is (= (pack [:a :a :b :b :c]) '((:a :a) (:b :b) (:c))))
  (is (= (pack [[1 2] [1 2] [3 4]]) '(([1 2] [1 2]) ([3 4])))))
  
(defn dupseq [s]
  (interleave s s))

(deftest test-32
  (is (= (dupseq [1 2 3]) '(1 1 2 2 3 3)))
  (is (= (dupseq [:a :a :b :b]) '(:a :a :a :a :b :b :b :b)))
  (is (= (dupseq [[1 2] [3 4]]) '([1 2] [1 2] [3 4] [3 4])))
  (is (= (dupseq [44 33]) [44 44 33 33])))
  
(defn splatter [s n]
  (mapcat
    (fn [s]
      (repeat n s))s))

(deftest test-33
  (is (= (splatter [1 2 3] 2) '(1 1 2 2 3 3)))
  (is (= (splatter [:a :b] 4) '(:a :a :a :a :b :b :b :b)))
  (is (= (splatter [4 5 6] 1) '(4 5 6)))
  (is (= (splatter [[1 2] [3 4]] 2) '([1 2] [1 2] [3 4] [3 4])))
  (is (= (splatter [44 33] 2) [44 44 33 33])))
  
(defn myrange [a b]
  (take (- b a) (iterate inc a)))

(deftest test-34
  (is (= (myrange 1 4) '(1 2 3)))
  (is (= (myrange -2 2) '(-2 -1 0 1)))
  (is (= (myrange 5 8) '(5 6 7))))

(run-tests)
</code></pre>
